
NF == 4 {
  account = $1
  amount = $2
  children = $3
  descendants = $4

  if (!amount && !empty)
    next
  if (flat && !depth && descendants != 0)
    next
  levels = split(account, accounts, /:/)
  if (depth && levels > depth)
    next
  if (flat && depth && levels < depth)
    next
  if (depth && depth == levels + 1 && children == 1) {
    flat_override++
    next
  }
  if (depth != levels && descendants == 1) {
    flat_override++
    next
  }

  if (!flat && flat_override) {
    indent = levels - flat_override
    account = accounts[levels]
    for (i = levels - 1; i >= indent; i--)
        account = accounts[i] ":" account
  } else if (flat) {
    indent = 1
  } else {
    indent = levels
    gsub(/.*:/, "", account)
  }

  flat_override = 0

  printf "%20s%*s%s\n", sprintf("$%0.2f", amount), 2 * indent, "", account
}

# total
NF == 1 {
  printf("--------------------\n")
  printf("%20s\n", sprintf("%0.2f", $1))
}

